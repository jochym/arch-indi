# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=libindi
pkgver=2.2.2
pkgrel=1
pkgdesc='A distributed control protocol designed to operate astronomical instrumentation'
url='https://www.indilib.org/index.php?title=Main_Page'
license=(LGPL-2.1-only)
arch=(x86_64)
depends=(cblas
         cfitsio
         curl
         fftw
         glibc
         gsl
         libev
         libgcc
         libjpeg-turbo
         libnova
         erfa
         libogg
         libstdc++
         libtheora
         libusb
         libxisf
         rtl-sdr
         systemd-libs
         zlib)
makedepends=(cmake
             git
             qt6-base)
optdepends=('qt6-base: Qt client library')
source=(git+https://github.com/indilib/indi#tag=v$pkgver)
sha256sums=('0f35ffeedacbade9a0aec9a9bf0397692e22f9c2684c9bb4e0e3741418544f2c')

prepare() {
  git -C indi cherry-pick -n bf77bd1c26c268e5973cace1dc21807b42148539 # Fix build
}

build() {
  cmake -B build -S indi \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DINDI_BUILD_QT_CLIENT=ON \
    -DUDEVRULES_INSTALL_DIR=/usr/lib/udev/rules.d \
    -DFIX_WARNINGS=OFF \
    -DCMAKE_C_FLAGS="$CFLAGS -ffat-lto-objects" \
    -DCMAKE_CXX_FLAGS="$CXXFLAGS -ffat-lto-objects -Wp,-U_GLIBCXX_ASSERTIONS"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
